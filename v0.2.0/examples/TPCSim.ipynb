{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Simulation of the Aleph TPC\n",
    "\n",
    "In this example we demonstrate how to connect the [PYTHIA](https://pythia.org) MC generator to a\n",
    "to a Geant4 application that implements a very simplistic model of the ALEPH TPC.\n",
    "\n",
    "The TPC detector is a simple cylinder filled with a mixture of Argon and Methane gas inside\n",
    "an axial 1.5 tesla magnetic field. The primary particles are generated by PYTHIA8 with the\n",
    "the LEP collision settings."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Loading the necessary Julia modules\n",
    "- `Geant4` and `Geant4.SystemOfUnits` for the Geant4 simulation\n",
    "- `Parameters` for the parameter handling in the detector definition\n",
    "- `PYTHIA8` for the PYTHIA8 generator"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Geant4\n",
    "using Geant4.SystemOfUnits\n",
    "using Parameters\n",
    "using PYTHIA8\n",
    "\n",
    "# to force loading G4Vis extension we need to load the following modules\n",
    "using CairoMakie, Rotations, IGLWrap_jll, LinearAlgebra\n",
    "using PYTHIA8: px, py, pz\n",
    "import DisplayAs: PNG"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Define the TPC Detector\n",
    "The TPC detector is a simple cylinder filled with a mixture of Ar and CH4 gas"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Geant4.SystemOfUnits: m, cm, mole,cm3"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The data structure `AlephTPC` is defined with the default detector parameters"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "@with_kw mutable struct AlephTPC <: G4JLDetector\n",
    "    totalL::Float64 = 4.4m      # total length of the TPC\n",
    "    innerR::Float64 = 0.31m     # inner radius of the TPC\n",
    "    outerR::Float64 = 1.8m      # outer radius of the TPC\n",
    "    checkOverlaps::Bool = false # do check overlaps when creating the geometry\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The function `construct` is defined to create the geometry of the TPC detector. It receives\n",
    "an instance of the `AlephTPC` structure and returns a pointer to the physical volume of the world."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function construct(det::AlephTPC)::CxxPtr{G4VPhysicalVolume}\n",
    "    (; totalL, innerR, outerR, checkOverlaps) = det\n",
    "\n",
    "    ##---Materials----------------------------------------------------------------------------------\n",
    "    nist = G4NistManager!Instance()\n",
    "    m_Ar     = FindOrBuildMaterial(nist, \"G4_Ar\")\n",
    "    m_CH₄    = FindOrBuildMaterial(nist, \"G4_METHANE\")\n",
    "    ArCH₄  = G4Material(\"ArCH₄\", 3.491*mg/cm3, 2)\n",
    "    AddMaterial( ArCH₄, m_Ar, 0.91 )\n",
    "    AddMaterial( ArCH₄, m_CH₄,0.09 )\n",
    "    m_ArCH₄  = FindOrBuildMaterial(nist, \"ArCH₄\")\n",
    "    m_air    = FindOrBuildMaterial(nist, \"G4_AIR\")\n",
    "    m_Al     = FindOrBuildMaterial(nist, \"G4_Al\")\n",
    "\n",
    "    ##---Volumes------------------------------------------------------------------------------------\n",
    "    worldZHalfLength = 1.1 * totalL/2\n",
    "    worldS  = G4Box(\"world\", worldZHalfLength, worldZHalfLength, worldZHalfLength)\n",
    "    worldLV = G4LogicalVolume(worldS, m_air, \"World\")\n",
    "    worldPV = G4PVPlacement(nothing, G4ThreeVector(), worldLV, \"World\", nothing, false, 0, checkOverlaps)\n",
    "\n",
    "    fcOuterS  = G4Tubs(\"fcOuter\", outerR, outerR + 1.8cm, totalL/2, 0, 2π)\n",
    "    fcOuterLV = G4LogicalVolume(fcOuterS, m_Al, \"fcOuter\")\n",
    "    fcInnerS  = G4Tubs(\"fcInner\", innerR - 0.8cm, innerR, totalL/2, 0, 2π)\n",
    "    fcInnerLV = G4LogicalVolume(fcInnerS, m_Al, \"fcInner\")\n",
    "\n",
    "    chamberS  = G4Tubs(\"chamber\", innerR, outerR, totalL/2, 0, 2π)\n",
    "    chamberLV = G4LogicalVolume(chamberS, m_ArCH₄, \"chamber\")\n",
    "    G4PVPlacement(nothing, G4ThreeVector(), fcOuterLV, \"fcOuter\", worldLV, false, 0, checkOverlaps)\n",
    "    G4PVPlacement(nothing, G4ThreeVector(), fcInnerLV, \"fcInner\", worldLV, false, 0, checkOverlaps)\n",
    "    G4PVPlacement(nothing, G4ThreeVector(), chamberLV, \"chamber\", worldLV, false, 0, checkOverlaps)\n",
    "    ##SetUserLimits(chamberLV, G4UserLimits(5mm))\n",
    "\n",
    "    ##---Visualization attributes------------------------------------------------------------------\n",
    "    boxVisAtt = G4VisAttributes(G4Colour(1.0, 1.0, 1.0, 0.0))\n",
    "    chamberVisAtt = G4VisAttributes(G4Colour(0.0, 1.0, 1.0, 0.2))\n",
    "    fcVisAtt = G4VisAttributes(G4Colour(0.0, 1.0, 0.0, 0.1))\n",
    "    SetVisAttributes(worldLV, boxVisAtt)\n",
    "    SetVisAttributes(chamberLV, chamberVisAtt)\n",
    "    SetVisAttributes(fcInnerLV, fcVisAtt)\n",
    "    SetVisAttributes(fcOuterLV, fcVisAtt)\n",
    "\n",
    "    ##---Always return the physical world-------------------------------------------------------------\n",
    "    return worldPV\n",
    "end\n",
    "Geant4.getConstructor(::AlephTPC)::Function = construct"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Finally, the detector is created with the default parameters"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "tpc = AlephTPC()          # create the detector"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Magnetic Field initialization"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "bfield = G4JLUniformMagField(G4ThreeVector(0,0, 1.5tesla))"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Primary Particle Generator"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "@with_kw mutable struct PythiaGenerator <: G4JLGeneratorData\n",
    "    eCM::Float64 = 91.2*GeV\n",
    "    pythia = nothing\n",
    "end\n",
    "\n",
    "function LEPCollision()\n",
    "    data = PythiaGenerator()\n",
    "    function LEPinit(data::PythiaGenerator, app::G4JLApplication)\n",
    "        data.pythia = PYTHIA8.Pythia(\"\", false)\n",
    "        data.pythia << \"Beams:idA = -11\" # positron\n",
    "        data.pythia << \"Beams:idB = 11\"  # electron\n",
    "        data.pythia << \"Beams:eCM = $(data.eCM/GeV)\" # CM energy\n",
    "        data.pythia << \"PDF:lepton = off\" # no lepton PDFs\n",
    "        # Process: hadronic decays of Z0. Initialize. Histogram.\n",
    "        data.pythia << \"WeakSingleBoson:ffbar2gmZ = on\"\n",
    "        data.pythia << \"23:onMode = off\"\n",
    "        data.pythia << \"23:onIfAny = 1 2 3 4 5\"\n",
    "        # If Pythia fails to initialize, exit with error.\n",
    "        data.pythia |> init || error(\"Pythia initialization failed\")\n",
    "    end\n",
    "    function LEPgenerate(g4evt::G4Event, data::PythiaGenerator)::Nothing\n",
    "        # Generate event. Skip if error. List first one.\n",
    "        data.pythia |> next || return\n",
    "        evt = data.pythia |> event\n",
    "        # Define primary vertex\n",
    "        vertex = G4PrimaryVertex(G4ThreeVector(0,0,0), 0)\n",
    "        # Loop over the particles in the Pythia event\n",
    "        for p in evt\n",
    "            if isFinal(p)\n",
    "                primary = G4PrimaryParticle(p |> id, px(p)*GeV, py(p)*GeV, pz(p)*GeV)\n",
    "                SetPrimary(vertex, move!(primary))    ## note that we give up ownership of the objects just created\n",
    "            end\n",
    "        end\n",
    "        AddPrimaryVertex(g4evt, move!(vertex))  ## note that we give up ownership of the objects just created\n",
    "    end\n",
    "    G4JLPrimaryGenerator(\"LEPCollision\", data; init_method=LEPinit, generate_method=LEPgenerate)\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Event Display\n",
    "Overwriting the default `drawEvent` function defined in the G4Vis extension"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "const G4Vis = Base.get_extension(Geant4, :G4Vis)\n",
    "function G4Vis.drawEvent(evtdisp::G4Vis.G4JLEventDisplay)\n",
    "    println(\"Drawing event\", typeof(evtdisp))\n",
    "    s = evtdisp.lscene.scene\n",
    "    settings = evtdisp.settings\n",
    "    trajectories = G4EventManager!GetEventManager() |> GetConstCurrentEvent |> GetTrajectoryContainer\n",
    "    if trajectories != C_NULL\n",
    "        points = Point3{Float64}[]\n",
    "        for t in trajectories\n",
    "            t |> GetCharge == 0 && continue # skip neutral particles\n",
    "            npoints = GetPointEntries(t)\n",
    "            npoints < 3 && continue         # skip particles with less than 3 points\n",
    "            for i in 1:npoints\n",
    "                pos = GetPoint(t, i-1) |> GetPosition\n",
    "                r = sqrt(pos[0]^2 + pos[1]^2)\n",
    "                z = pos[2]\n",
    "                (r > 1.8m || abs(z) > 2.4m) && break  # kill particle outside the TPC\n",
    "                if r > 0.38m                # skip points inside the beam pipe\n",
    "                    push!(points, convert(Point3{Float64}, pos))\n",
    "                else\n",
    "                    push!(points, Point3{Float64}(NaN, NaN, NaN))\n",
    "                end\n",
    "            end\n",
    "            push!(points, Point3{Float64}(NaN, NaN, NaN))\n",
    "        end\n",
    "        lines!(s, points, color=settings.trajectories.color, transparency=false, overdraw=false)\n",
    "    end\n",
    "    ##wait_for_key(\"Press any key to continue with next event\")\n",
    "end\n",
    "evtdisplay = G4JLEventDisplay(joinpath(@__DIR__, \"TPCSimVisSettings.jl\"))"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Create the Application"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "app = G4JLApplication(detector     = tpc,                       # detector defined above\n",
    "                      generator    = LEPCollision(),            # primary generator to instantiate\n",
    "                      field        = bfield,                    # magnetic field\n",
    "                      physics_type = FTFP_BERT,                 # what physics list to instantiate\n",
    "                      evtdisplay   = evtdisplay                 # event display\n",
    "                     );\n",
    "configure(app)\n",
    "initialize(app)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Run the Application"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "beamOn(app, 1)\n",
    "PNG(evtdisplay.figure)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Another event"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "beamOn(app, 1)\n",
    "PNG(evtdisplay.figure)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.1"
  },
  "kernelspec": {
   "name": "julia-1.11",
   "display_name": "Julia 1.11.1",
   "language": "julia"
  }
 },
 "nbformat": 4
}
