{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Basic/B4a Example\n",
    "\n",
    "This example simulates a simple Sampling Calorimeter setup.\n",
    "To demonstrate several possible ways of data scoring, the example is provided in four variants: %B4a, %B4b, %B4c, %B4d.\n",
    "See [README](https://github.com/Geant4/geant4/blob/master/examples/basic/B5/README.md)\n",
    "file for the example."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Loading the necessary Julia modules\n",
    "Load the `Geant4` and `Geant4.SystemOfUnits` modules."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Geant4\n",
    "using Geant4.SystemOfUnits:  cm, cm3, mm, pGy, eplus, keV, g, eV, MeV, mole\n",
    "using FHist, Plots\n",
    "\n",
    "# to force loading G4Vis extension we need to load the following module\n",
    "using CairoMakie"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Define Detector Parameters struct\n",
    "The `B4Detector` structure is defined with the default detector parameters."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "mutable struct B4aDetector <: G4JLDetector\n",
    "    # main input parameters\n",
    "    const nofLayers::Int\n",
    "    const checkOverlaps::Bool\n",
    "    const absoThickness::Float64\n",
    "    const gapThickness::Float64\n",
    "    const calorSizeXY::Float64\n",
    "\n",
    "    # constructor with defaults values for parameters\n",
    "    function B4aDetector(;      nofLayers::Int=10,\n",
    "                                checkOverlaps::Bool=true,\n",
    "                                absoThickness::Float64=10mm,\n",
    "                                gapThickness::Float64=5mm,\n",
    "                                calorSizeXY::Float64=10cm)\n",
    "            self = new(nofLayers, checkOverlaps, absoThickness, gapThickness, calorSizeXY)\n",
    "        return self\n",
    "    end\n",
    "end\n",
    "_layerThickness(d::B4aDetector) = d.absoThickness + d.gapThickness\n",
    "_calorThickness(d::B4aDetector) = d.nofLayers * _layerThickness(d)\n",
    "_worldSizeZ(d::B4aDetector) = 1.2 * _calorThickness(d)\n",
    "_worldSizeXY(d::B4aDetector) = 1.2 * d.calorSizeXY"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Defining the geometry constructor"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function B4aConstruct(det::B4aDetector)::CxxPtr{G4VPhysicalVolume}\n",
    "    (; nofLayers, checkOverlaps, absoThickness, gapThickness, calorSizeXY) = det\n",
    "    nist = G4NistManager!Instance()\n",
    "\n",
    "\n",
    "    layerThickness = _layerThickness(det)\n",
    "    calorThickness = _calorThickness(det)\n",
    "    worldSizeXY = _worldSizeXY(det)\n",
    "    worldSizeZ = _worldSizeZ(det)\n",
    "\n",
    "    G4Material(\"liquidArgon\", z = 18., a = 30.95g/mole, density = 1.390g/cm3)\n",
    "\n",
    "    defaultMaterial = FindOrBuildMaterial(nist, \"G4_Galactic\")\n",
    "    absorberMaterial = FindOrBuildMaterial(nist, \"G4_Pb\")\n",
    "    gapMaterial = FindOrBuildMaterial(nist, \"liquidArgon\")\n",
    "\n",
    "\n",
    "\n",
    "    worldS = G4Box(\"World\", 0.5 * worldSizeXY, 0.5 * worldSizeXY, 0.5 * worldSizeZ)\n",
    "\n",
    "    worldLV = G4LogicalVolume(worldS, defaultMaterial, \"WorldLV\")\n",
    "\n",
    "    worldPV = G4PVPlacement(nothing,          # no rotation\n",
    "                            G4ThreeVector(),  # at (0,0,0)\n",
    "                            worldLV,          # its logical volume\n",
    "                            \"World\",          # its name\n",
    "                            nothing,          # its mother volume\n",
    "                            false,            # no boolean operation\n",
    "                            0,                # copy number\n",
    "                            checkOverlaps)    # overlaps checking\n",
    "\n",
    "##---------------------------------------\n",
    "    calorimeterS = G4Box(\"Calorimeter\", 0.5 * calorSizeXY, 0.5 * calorSizeXY, 0.5 * calorThickness)\n",
    "\n",
    "\n",
    "    calorLV = G4LogicalVolume(calorimeterS, defaultMaterial, \"CalorLV\")\n",
    "\n",
    "    println(calorLV)\n",
    "\n",
    "    G4PVPlacement(nothing,\n",
    "                    G4ThreeVector(),\n",
    "                    calorLV,\n",
    "                    \"CalorPV\",\n",
    "                    worldLV,\n",
    "                    false,\n",
    "                    0,\n",
    "                    checkOverlaps)\n",
    "##----------------------------------------------\n",
    "\n",
    "     layerS = G4Box(\"Layer\",\n",
    "                    0.5*calorSizeXY, 0.5*calorSizeXY, 0.5*layerThickness)\n",
    "\n",
    "     layerLV = G4LogicalVolume(layerS,\n",
    "                           defaultMaterial,\n",
    "                           \"Layer\")\n",
    "\n",
    "    G4PVReplica(\"Layer\",\n",
    "                  layerLV,\n",
    "                  calorLV,\n",
    "                  kZAxis,\n",
    "                  nofLayers,\n",
    "                  layerThickness)\n",
    "\n",
    "##---------------------------------------------------\n",
    "   absorberS = G4Box(\"Abso\",\n",
    "                      0.5*calorSizeXY, 0.5*calorSizeXY, 0.5*absoThickness)\n",
    "\n",
    "    absorberLV = G4LogicalVolume(absorberS, absorberMaterial, \"AbsoLV\")\n",
    "\n",
    "    G4PVPlacement(nothing,\n",
    "                  G4ThreeVector(0., 0., -0.5*gapThickness),\n",
    "                    absorberLV,\n",
    "                    \"AbsPV\",\n",
    "                    layerLV,\n",
    "                    false,\n",
    "                    0,\n",
    "                    checkOverlaps)\n",
    "##--------------------------------------\n",
    "\n",
    "  gapS = G4Box(\"Gap\", 0.5*calorSizeXY, 0.5*calorSizeXY, 0.5*gapThickness)\n",
    "\n",
    "  gapLV = G4LogicalVolume(gapS, gapMaterial, \"Gap\")\n",
    "\n",
    "  G4PVPlacement(nothing,\n",
    "                G4ThreeVector(0., 0., 0.5*absoThickness),\n",
    "                gapLV,\n",
    "                \"GapPV\",\n",
    "                layerLV,\n",
    "                false,\n",
    "                0,\n",
    "                checkOverlaps)\n",
    "\n",
    "\n",
    "##------------------------------------------\n",
    "##Visualization attributes\n",
    "    SetVisAttributes(worldLV, G4VisAttributes!GetInvisible())\n",
    "    SetVisAttributes(calorLV, G4VisAttributes!GetInvisible())\n",
    "\n",
    "return worldPV              # return a pointer to the G4PhysicalVolume\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Instantiate the detector"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "detector = B4aDetector()"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Primary Particle Generator"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "worldZHalfLength = _worldSizeZ(detector)/2\n",
    "particlegun = G4JLGunGenerator(particle = \"e-\",\n",
    "                               energy = 300MeV,\n",
    "                               direction = G4ThreeVector(0., 0., 1.),\n",
    "                               position = G4ThreeVector(0,0,-worldZHalfLength))"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Defining the simulation data"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "const Hist1D64 = Hist1D{Float64}\n",
    "mutable struct B4aSimData <: G4JLSimulationData\n",
    "    fEnergyDeposit_Abs::Float64\n",
    "    fEnergyDeposit_Gap::Float64\n",
    "\n",
    "    fStepLength_Abs::Float64\n",
    "    fStepLength_Gap::Float64\n",
    "\n",
    "    fEdepHist_Abs::Hist1D64\n",
    "    fStepLenHist_Abs::Hist1D64\n",
    "\n",
    "    fEdepHist_Gap::Hist1D64\n",
    "    fStepLenHist_Gap::Hist1D64\n",
    "\n",
    "    B4aSimData() = new()\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## User Actions\n",
    "## Begin Run Action"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function beginrun(run::G4Run, app::G4JLApplication)::Nothing\n",
    "    data = getSIMdata(app)\n",
    "\n",
    "    data.fEdepHist_Abs = Hist1D(;binedges=100.:1.:300.)\n",
    "    data.fStepLenHist_Abs = Hist1D(;binedges=0.:2.:500.)\n",
    "\n",
    "    data.fEdepHist_Gap = Hist1D(;binedges=0.:1.:200.)\n",
    "    data.fStepLenHist_Gap = Hist1D(;binedges=0.:2.:500.)\n",
    "    nothing\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### End Run Action"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function endrun(run::G4Run, app::G4JLApplication)::Nothing\n",
    "    nothing\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Begin Event Action"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function beginevent(evt::G4Event, app::G4JLApplication)\n",
    "    G4JL_println(\"===============started event $(evt |> GetEventID)\")\n",
    "    data = getSIMdata(app)\n",
    "    data.fEnergyDeposit_Abs = 0.0\n",
    "    data.fStepLength_Abs = 0.0\n",
    "\n",
    "    data.fEnergyDeposit_Gap = 0.0\n",
    "    data.fStepLength_Gap = 0.0\n",
    "    return\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "###End event action"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function endevent(evt::G4Event, app::G4JLApplication)\n",
    "    data = getSIMdata(app)\n",
    "#     G4JL_println(\"AbsEdep: $(data.fEnergyDeposit_Abs)\")\n",
    "#     println(\"AbsStepLength: \", data.fStepLength_Abs)\n",
    "\n",
    "    push!(data.fEdepHist_Abs, data.fEnergyDeposit_Abs)\n",
    "    push!(data.fStepLenHist_Abs, data.fStepLength_Abs)\n",
    "\n",
    "    push!(data.fEdepHist_Gap, data.fEnergyDeposit_Gap)\n",
    "    push!(data.fStepLenHist_Gap, data.fStepLength_Gap)\n",
    "\n",
    "    println(\"Total Energy deposited in Absorber: \",data.fEnergyDeposit_Abs)\n",
    "    println(\"Total Energy deposited in Gaps: \", data.fEnergyDeposit_Gap)\n",
    "    G4JL_println(\"================event ended $(evt |> GetEventID) \\n\")\n",
    "    return\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Stepping Action"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function stepaction(step::G4Step, app::G4JLApplication)\n",
    "    data = getSIMdata(app)\n",
    "\n",
    "    volume = step |> GetPreStepPoint |> GetPhysicalVolume |> GetName\n",
    "    edep = step |> GetTotalEnergyDeposit\n",
    "\n",
    "    stepLength = 0\n",
    "    if step |> GetTrack |> GetDefinition |> GetPDGCharge != 0.\n",
    "        stepLength = step |> GetStepLength\n",
    "    end\n",
    "\n",
    "    if volume[] == \"AbsPV\"\n",
    "        data.fEnergyDeposit_Abs += edep\n",
    "        data.fStepLength_Abs += stepLength\n",
    "    end\n",
    "\n",
    "    if volume[] == \"GapPV\"\n",
    "        data.fEnergyDeposit_Gap += edep\n",
    "        data.fStepLength_Gap += stepLength\n",
    "    end\n",
    "    return\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Making the Application"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "evtdisplay   = G4JLEventDisplay(joinpath(@__DIR__, \"B4aVis.jl\"))\n",
    "Geant4.getConstructor(::B4aDetector)::Function = B4aConstruct\n",
    "app = G4JLApplication(detector = detector,               # detector with parameters\n",
    "                      simdata = B4aSimData(),            # simulation data\n",
    "                      generator = particlegun,           # primary particle generator\n",
    "                      nthreads = 0,                      # # of threads (0 = no MT)\n",
    "                      physics_type = FTFP_BERT,          # what physics list to instantiate\n",
    "                      evtdisplay   = evtdisplay,         # event display\n",
    "                      stepaction_method = stepaction,\n",
    "                      beginrunaction_method = beginrun,  # begin-run action (initialize counters and histograms)\n",
    "                      endrunaction_method = endrun,\n",
    "                      begineventaction_method=beginevent,\n",
    "                      endeventaction_method = endevent\n",
    "                     )\n",
    "\n",
    "configure(app)\n",
    "initialize(app)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Run the Example\n",
    "Run a single event and display it"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "# ui`/tracking/verbose 1`\n",
    "beamOn(app,1)\n",
    "display(evtdisplay.figure)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Run for 100 events"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "beamOn(app, 100)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Plotting the histograms"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "data = getSIMdata(app)\n",
    "lay = @layout [째; 째; 째; 째]\n",
    "img = Plots.plot(layout=lay, show=true, size=(800,1000))\n",
    "\n",
    "Plots.plot!(subplot=1, data.fEdepHist_Abs, title=\"Total EDep in Absorber\", xlabel=\"Edep (MeV)\", label=\"Abs Edep\", show=true)\n",
    "Plots.plot!(subplot=2, data.fEdepHist_Gap, title=\"Total EDep in Gaps\", xlabel=\"Edep (MeV)\", label=\"Gap_Edep\", show=true)\n",
    "\n",
    "Plots.plot!(subplot=3, data.fStepLenHist_Abs, title=\"Total StepLeng in Absorbers\", xlabel=\"Step Length (mm)\", label=\"Abs_StepLen\", show=true)\n",
    "Plots.plot!(subplot=4, data.fStepLenHist_Gap, title=\"Total StepLeng in Gaps\", xlabel=\"Step Length (mm)\", label=\"Gap_StepLen\", show=true)\n",
    "PNG(img) #hide"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.2"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.2",
   "language": "julia"
  }
 },
 "nbformat": 4
}